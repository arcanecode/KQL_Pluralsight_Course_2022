// Example 1 - Case Basics
Perf
| where CounterName == "% Free Space"
| take 100
| extend FreeLevel = case( CounterValue < 10, "Critical"
                         , CounterValue < 30, "Danger"
                         , CounterValue < 50, "Look at it"
                         , "You're OK!"
                         )
| project Computer
        , CounterName
        , CounterValue
        , FreeLevel
| sort by CounterValue asc


// Alternate formatting
Perf
| where CounterName == "% Free Space"
| take 100
| project Computer
        , CounterName
        , CounterValue
        , FreeLevel = case( CounterValue < 10, "Critical", CounterValue < 30, "Danger", CounterValue < 50, "Look at it", "You're OK!")
| sort by CounterValue asc


// Example 2 - Summarizing
Perf
| where CounterName == "% Free Space"
| extend FreeLevel = case( CounterValue < 10, "Critical (Less than 10% free disk space)"
                         , CounterValue < 30, "Danger (10% to 30% free disk space)"
                         , CounterValue < 50, "Look at it (30% to 50% free disk space)"
                         , "You're OK! (More than 50% free disk space)"
                         )
| summarize ComputerCount=count()
         by FreeLevel

// Example 3 - Dates - The Wrong Way
Perf
| where CounterName == "% Processor Time" and CounterValue > 0
| where TimeGenerated >= startofyear( now() )
| project TimeGenerated, Computer
| extend MonthNumber = datetime_part("month", TimeGenerated)
       , YearNumber =  datetime_part("year", TimeGenerated)
       , MonthName = case( datetime_part("month", TimeGenerated) ==  1, "Jan "
                         , datetime_part("month", TimeGenerated) ==  2, "Feb "
                         , datetime_part("month", TimeGenerated) ==  3, "Mar "
                         , datetime_part("month", TimeGenerated) ==  4, "Apr "
                         , datetime_part("month", TimeGenerated) ==  5, "May "
                         , datetime_part("month", TimeGenerated) ==  6, "Jun "
                         , datetime_part("month", TimeGenerated) ==  7, "Jul "
                         , datetime_part("month", TimeGenerated) ==  8, "Aug "
                         , datetime_part("month", TimeGenerated) ==  9, "Sep "
                         , datetime_part("month", TimeGenerated) == 10, "Oct "
                         , datetime_part("month", TimeGenerated) == 11, "Nov "
                         , datetime_part("month", TimeGenerated) == 12, "Dec "
                         , "Unknown Month"
                         )
| summarize MonthCount = count()
         by Computer, YearNumber, MonthNumber, MonthName
| project Computer, MonthCount, YearNumber, MonthNumber
        , YearText = strcat(YearNumber)
        , MonthName
        , MonthYear = strcat(MonthName, " ", YearNumber )
        , YearMonth = strcat(YearNumber, " - ", MonthName )
| sort by Computer, YearNumber, MonthNumber


// Example 4 - Dates - The Right Way
Perf
| where CounterName == "% Processor Time" and CounterValue > 0
| where TimeGenerated >= startofyear( now() )
| project TimeGenerated, Computer
| extend MonthNumber = datetime_part("month", TimeGenerated)
       , YearNumber =  datetime_part("year", TimeGenerated)
| extend MonthName = case( MonthNumber ==  1, "Jan"
                         , MonthNumber ==  2, "Feb"
                         , MonthNumber ==  3, "Mar"
                         , MonthNumber ==  4, "Apr"
                         , MonthNumber ==  5, "May"
                         , MonthNumber ==  6, "Jun"
                         , MonthNumber ==  7, "Jul"
                         , MonthNumber ==  8, "Aug"
                         , MonthNumber ==  9, "Sep"
                         , MonthNumber == 10, "Oct"
                         , MonthNumber == 11, "Nov"
                         , MonthNumber == 12, "Dec"
                         , "Unknown"
                         )
| summarize MonthCount = count()
         by Computer, YearNumber, MonthNumber, MonthName
| project Computer
        , MonthCount
        , YearNumber
        , MonthNumber
        , YearText = strcat(YearNumber)
        , MonthName
        , MonthYear = strcat(MonthName, " ", YearNumber )
        , YearMonth = strcat(YearNumber, " - ", MonthName )
| sort by Computer, YearNumber, MonthNumber
