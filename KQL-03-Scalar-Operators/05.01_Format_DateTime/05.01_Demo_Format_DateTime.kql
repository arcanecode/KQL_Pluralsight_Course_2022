// Example 1 - Format_DateTime Basics
Perf
| where TimeGenerated >= ago(60d)
| take 100
| project TimeGenerated
        , D1 = format_datetime(TimeGenerated, "y-M-d")
        , D2 = format_datetime(TimeGenerated, "yyyy-MM-dd")
        , D3 = format_datetime(TimeGenerated, "MM/dd/yyyy")
        , T1 = format_datetime(TimeGenerated, "MM/dd/yyyy hh:mm:ss tt")
        , T2 = format_datetime(TimeGenerated, "MM/dd/yyyy HH:mm:ss")
        , T3 = format_datetime(TimeGenerated, "MM/dd/yyyy HH:mm:ss.ffff")
        , CounterName
        , CounterValue

//------------------------------------------------------------------------------
// Supported syntax - one letter is single number, two letters two numbers
//    d - Day, 1 to 31
//   dd - Day, 01 to 31
//    M - Month, 1 to 12
//   MM - Month, 01 to 12
//    y - Year, 0 to 9999
//   yy - Year, 00 to 9999
// yyyy - Year, 0000 to 9999

// Hours and subsceonds can also be used with format_timespan (see below)
//  h - Hour, 1 to 12
// hh - Hour, 01 to 12
//  H - Hour, 1 to 23
// HH - Hour, 01 to 23
//  m - Minute, 0 to 59
// mm - Minute, 00 to 59
//  s - Second, 0 to 59
// ss - Second, 00 to 59
// tt - am/pm

// f/F can also be used for subseconds. Lowercase f will always display a 0,
// uppercase F's will only display a number if there's a subsecond value.
// Otherwise nothing is displayed.
// The Number of f's indicates the precesion down to millionth's of a second


// You can use these as separators:
// / - : , . _ [ ] and a space
//------------------------------------------------------------------------------

// Experiment with formats using print
print D1 = format_datetime(now(), "y-M-d")
    , D2 = format_datetime(now(), "yyyy-MM-dd")
    , D3 = format_datetime(now(), "MM/dd/yyyy")
    , T1 = format_datetime(now(), "MM/dd/yyyy hh:mm:ss tt")
    , T2 = format_datetime(now(), "MM/dd/yyyy HH:mm:ss")
    , T3 = format_datetime(now(), "MM/dd/yyyy HH:mm:ss.ffff")

// Example 2 - Separating Date and Time
Perf
| take 20
| project TimeGenerated
        , TheDate = format_datetime(TimeGenerated, "yyyy-MM-dd")
        , TheTime = format_datetime(TimeGenerated, "hh:mm:ss tt")
        , CounterName
        , CounterValue


// Example 3 - Breaking into component parts
Perf
| take 20
| project TimeGenerated
        , Year   = format_datetime(TimeGenerated, "yyyy")
        , Month  = format_datetime(TimeGenerated, "MM")
        , Day    = format_datetime(TimeGenerated, "dd")
        , Hour   = format_datetime(TimeGenerated, "HH")
        , Minute = format_datetime(TimeGenerated, "mm")
        , Second = format_datetime(TimeGenerated, "ss")
        , CounterName
        , CounterValue
