// Example 1 - DateTime Arithmetic Basics
Perf
| where CounterName == "Avg. Disk sec/Read"
| where TimeGenerated > ago(1d)
| take 10
| extend HowLongAgo=( now() - TimeGenerated )
       , RightNow = now()
| project TimeGenerated
        , RightNow
        , HowLongAgo
        , Computer
        , CounterName
        , CounterValue

// Example 2 - Time since a set date
Perf
| where CounterName == "Avg. Disk sec/Read"
| where TimeGenerated > ago(1d)
| take 10
| extend HowLongAgo=( now() - TimeGenerated )
       , TimeSinceStartOfYear=( TimeGenerated - datetime(2022-01-01) )
       , RightNow = now()
| project TimeGenerated
        , RightNow
        , HowLongAgo
        , TimeSinceStartOfYear
        , Computer
        , CounterName
        , CounterValue

// Example 3 - Calculating in a specific timespan unit
Perf
| where CounterName == "Avg. Disk sec/Read"
| where CounterValue > 0
| take 100                       // done just to give us a small dataset to demo
| extend HowLongAgo=( now() - TimeGenerated )
       , TimeSinceStartOfYear=( TimeGenerated - datetime(2022-01-01) )
| extend TimeSinceStartOfYearInHours=( TimeSinceStartOfYear / 1h )
| project Computer
        , CounterName
        , CounterValue
        , TimeGenerated
        , HowLongAgo
        , TimeSinceStartOfYear
        , TimeSinceStartOfYearInHours

// Example 4 - DateTime Arithmetic on Columns
Perf
| take 100
| extend BucketTime = (BucketEndTime - BucketStartTime)
| project TimeGenerated
        , BucketTime
        , CounterName
        , CounterValue

Usage
| take 100
| project TimeGenerated
        , StartTime
        , EndTime
        , ElapsedTime = (EndTime - StartTime)
        , Computer