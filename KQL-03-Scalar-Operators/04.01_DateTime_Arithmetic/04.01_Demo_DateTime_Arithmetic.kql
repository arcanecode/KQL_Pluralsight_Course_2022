//------------------------------------------------------------------------------
// Fun With KQL - DateTime Arithmetic
//
// The demos in this post are part of my blog post,
// Fun With KQL - DateTime Arithmetic
// https
//
// Copyright (c) 2018, 2022
// All rights reserved. This code may be used in part within your own
// applications.
//
// All demos are licensed under the MIT license. No warranty is provided.
// Use at your own risk and discretion.
//------------------------------------------------------------------------------

// Example 1 - DateTime Arithmetic Basics
Perf
| where CounterName == "Avg. Disk sec/Read"
| where CounterValue > 0
| take 100                       // done just to give us a small dataset to demo
| extend HowLongAgo=( now() - TimeGenerated )
       , RightNow = now()
| project TimeGenerated
        , RightNow
        , HowLongAgo
        , Computer
        , CounterName
        , CounterValue

// Example 2 - Time since a set date
Perf
| where CounterName == "Avg. Disk sec/Read"
| where CounterValue > 0
| take 100                       // done just to give us a small dataset to demo
| extend HowLongAgo=( now() - TimeGenerated )
       , TimeSinceStartOfYear=( TimeGenerated - datetime(2022-01-01) )
       , RightNow = now()
| project TimeGenerated
        , RightNow
        , HowLongAgo
        , TimeSinceStartOfYear
        , Computer
        , CounterName
        , CounterValue

// Example 3 - Calculating in a specific timespan unit
Perf
| where CounterName == "Avg. Disk sec/Read"
| where CounterValue > 0
| take 100                       // done just to give us a small dataset to demo
| extend HowLongAgo=( now() - TimeGenerated )
       , TimeSinceStartOfYear=( TimeGenerated - datetime(2022-01-01) )
| extend TimeSinceStartOfYearInHours=( TimeSinceStartOfYear / 1h )
| project Computer
        , CounterName
        , CounterValue
        , TimeGenerated
        , HowLongAgo
        , TimeSinceStartOfYear
        , TimeSinceStartOfYearInHours

// Example 4 - DateTime Arithmetic on Columns
Alert
| take 100
| extend QueryExecutionTime = (QueryExecutionEndTime - QueryExecutionStartTime)
| project QueryExecutionStartTime
        , QueryExecutionEndTime
        , QueryExecutionTime
        , Computer
        , AlertState
