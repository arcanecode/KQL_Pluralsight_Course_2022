// Example 1 - strcat is used to combine fields together
Perf
| take 100
| extend CompObjCounter = strcat(Computer, " - ", ObjectName, " - ", CounterName)
| project TimeGenerated
        , CompObjCounter
        , Computer
        , ObjectName
        , CounterName
        , CounterValue

// Example 2 - Use strcat with case and datetime_part to get month names
Perf
| where CounterName == "% Free Space"
| where TimeGenerated between ( ago(365d) .. now() )
| take 100
| extend MonthNumber = datetime_part("month", TimeGenerated)
| extend MonthName = case( MonthNumber ==  1, "Jan"
                         , MonthNumber ==  2, "Feb"
                         , MonthNumber ==  3, "Mar"
                         , MonthNumber ==  4, "Apr"
                         , MonthNumber ==  5, "May"
                         , MonthNumber ==  6, "Jun"
                         , MonthNumber ==  7, "Jul"
                         , MonthNumber ==  8, "Aug"
                         , MonthNumber ==  9, "Sep"
                         , MonthNumber == 10, "Oct"
                         , MonthNumber == 11, "Nov"
                         , MonthNumber == 12, "Dec"
                         , "Unknown"
                         )
| extend DateText = strcat( MonthName
                          , " "
                          , datetime_part("day", TimeGenerated)
                          , ", "
                          , datetime_part("year", TimeGenerated)
                          )
// Could have done on one line: strcat( MonthName, " ", datetime_part("day", TimeGenerated), ", ", datetime_part("year", TimeGenerated) )
| project TimeGenerated
        , MonthName
        , DateText
        , ComputerText = strcat("The computer name is ", Computer)
        , CounterName
        , CounterValue
        , FullText = strcat("On ", DateText, " the computer ", Computer, " had "
                           , CounterName, " of ", CounterValue
                           , " percent."
                           , iif( CounterValue < 90
                                , " Free up some space."
                                , " Have a nice day."
                                )
                           )
