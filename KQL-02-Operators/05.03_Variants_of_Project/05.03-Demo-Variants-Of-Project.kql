//------------------------------------------------------------------------------
// Kusto Query Language: Operators
// Demo - Variants of Project
//
// The demos in this folder are part of my Pluralsight course,
//   Kusto Query Language: Operators
//
// A full list of my Pluralsight Courses can be found at:
//   https://arcanecode.com/info/
//
// You can find additional information at my blog post, linked below.
//
// Fun With KQL - Variants of Project
// https://arcanecode.com/2022/06/06/fun-with-kql-variants-of-project/
//
// Copyright (c) 2018, 2022
// All rights reserved. This code may be used in part within your own
// applications.
//
// All demos are licensed under the MIT license. No warranty is provided.
// Use at your own risk and discretion.
//------------------------------------------------------------------------------

// Example 1 - Project-Away
Perf
| where TimeGenerated > ago(1h)
| take 100
| project-away SourceSystem
             , CounterPath
             , Max
             , Min
             , StandardDeviation
             , Type
             , _ResourceId
             , _SubscriptionId
             , TenantId
             , MG


// Example 2 - Project-Rename
Perf
| where CounterName == "Free Megabytes"
| take 100
| project ObjectName
        , CounterName
        , InstanceName
        , CounterValue
        , TimeGenerated
| project-rename FreeMB = CounterValue
| extend FreeGB = FreeMB / 1000
       , FreeKB = FreeMB * 1000

Perf
| where CounterName == "Free Megabytes"
| take 100
| project-rename FreeMB = CounterValue
| project ObjectName
        , CounterName
        , InstanceName
        , TimeGenerated
        , FreeGB = FreeMB / 1000
        , FreeMB
        , FreeKB = FreeMB * 1000


// Example 3 - Project-Keep
Perf
| where CounterName == "Free Megabytes"
| take 50
| project-keep
          ObjectName
        , CounterName
        , InstanceName
        , CounterValue
        , TimeGenerated


// Example 4 - Project-Keep with Wildcards
Perf
| where CounterName == "Free Megabytes"
| take 50
| project-keep
          ObjectName
        , InstanceName
        , Counter*
        , TimeGenerated


// Example 5 - Project-Reorder
Perf
| where CounterName == "Free Megabytes"
| take 50
| project-reorder ObjectName
        , CounterName
        , InstanceName
        , CounterValue
        , TimeGenerated


// Example 6 - Project-Reorder with Wildcards
Perf
| where CounterName == "Free Megabytes"
| take 50
| project-reorder ObjectName
        , Counter*
        , InstanceName
        , TimeGenerated


// Example 7 - Project-Reorder with Wildcards ASC
Perf
| where CounterName == "Free Megabytes"
| take 50
| project-reorder ObjectName
        , Counter* asc
        , InstanceName
        , TimeGenerated


// Example 8 - Project-Reorder All Columns with Wildcards ASC
Perf
| where CounterName == "Free Megabytes"
| take 50
| project-reorder * asc


// Example 9 - Project-Reorder All Columns with Wildcards DESC
Perf
| where CounterName == "Free Megabytes"
| take 50
| project-reorder * desc